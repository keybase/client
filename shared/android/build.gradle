// Top-level build file where you can add configuration options common to all sub-projects/modules.
// https://github.com/DylanVann/react-native-fast-image/issues/516
project.ext {
    excludeAppGlideModule = true
    glideVersion = "4.9.0"
}

def enableV8 = KB_USEV8 == 'true'

buildscript {
    ext {
        buildToolsVersion = "29.0.2"
        minSdkVersion = 16
        compileSdkVersion = 29
        targetSdkVersion = 28
        supportLibVersion = "28.0.0" // needed by some deps
     }

    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        // KB: this is an old version, could look into updating
        classpath 'com.github.triplet.gradle:play-publisher:2.1.1' // To publish from gradle
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

// KB
// Work around dependencies with old gradle versions depending on
// com.android.tools:common:25.3.3 , which disappeared from jcenter.
// Once these dependencies update their gradle version or otherwise fix
// the problem themselves, we can remove this.
//
// See https://github.com/DylanVann/react-native-fast-image/issues/366 .
subprojects {project ->
    if (project.name.contains('react-native-fast-image')) {
            buildscript {
                repositories {
                maven { url = 'https://dl.bintray.com/android/android-tools/'  }
            }
        }
    }
}

allprojects {
    repositories {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        if (enableV8) {
            maven {
                // Replace AAR from original RN with AAR from react-native-v8
                url("$rootDir/../node_modules/react-native-v8/dist")
            }
            maven {
                // prebuilt libv8.so
                url("$rootDir/../node_modules/v8-android-nointl/dist")
            }
        } else {
            maven {
                url("$rootDir/../node_modules/react-native/android")
            }
        }

        mavenLocal()
        if (!enableV8) {
            maven {
                // Android JSC is installed from npm
                url("$rootDir/../node_modules/jsc-android/dist")
            }
        }

        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}
