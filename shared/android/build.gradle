// Top-level build file where you can add configuration options common to all sub-projects/modules.
def enableV8 = KB_USEV8 == 'true'

buildscript {
    ext {
        buildToolsVersion = "30.0.2"
        minSdkVersion = 21
        compileSdkVersion = 30
        targetSdkVersion = 30
        ndkVersion = "23.1.7779620"
     }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:4.2.2")
        classpath("com.github.triplet.gradle:play-publisher:2.8.1") // To publish from gradle
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        google()
        maven { url 'https://www.jitpack.io' }
        mavenLocal()
        jcenter() {
            content {
                // TODO these are older deps that use the now deprecated jcenter repo. hopefully they'll be updated
                includeModule("com.yqritc", "android-scalablevideoview") // for react-native-video
                includeModule("com.theartofdev.edmodo", "android-image-cropper") // for expo-image-picker dep
                includeModule("com.google.android.exoplayer", "exoplayer") // for expo-av
                includeModule("com.google.android.exoplayer", "exoplayer-ui") // for expo-av
                includeModule("com.google.android.exoplayer", "exoplayer-smoothstreaming") // for expo-av
                includeModule("com.google.android.exoplayer", "extension-okhttp") // for expo-av
                includeModule("com.google.android.exoplayer", "exoplayer-core") // for expo-av
                includeModule("com.google.android.exoplayer", "exoplayer-dash") // for expo-av
                includeModule("com.google.android.exoplayer", "exoplayer-hls") // for expo-av
            }
        }
    }
}
