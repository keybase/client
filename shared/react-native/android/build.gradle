// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath 'com.github.triplet.gradle:play-publisher:1.1.5' // To publish from gradle

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        // Needed per https://github.com/zo0r/react-native-push-notification/issues/748#issuecomment-392523852 .
        google()
        maven {
            url 'https://jitpack.io'
        }
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../../node_modules/react-native/android"
        }
    }
}


ext {
     buildToolsVersion = "27.0.3"
     minSdkVersion = 16
     compileSdkVersion = 28
     targetSdkVersion = 28
     supportLibVersion = "26.1.0"
     googlePlayServicesVersion = "10.0.0"
 }

subprojects {
    project.configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support'
              && !details.requested.name.contains('multidex') ) {
                details.useVersion "26.1.0"
            }
        }
    }

    afterEvaluate {
        project -> if (project.hasProperty("android")) {
            android {
              compileSdkVersion rootProject.ext.compileSdkVersion
              defaultConfig {
                  targetSdkVersion rootProject.ext.targetSdkVersion
              }
            }
        }
    }
}
